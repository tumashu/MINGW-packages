# Maintainer: Feng Shu <tumashu@163.com>

_realname=marisa-trie
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=0.2.5
pkgrel=1
pkgdesc="MARISA: Matching Algorithm with Recursively Implemented StorAge (mingw-w64)"
url="https://github.com/s-yata/marisa-trie"
arch=('any')
license=('BSD')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "autoconf")
source=("${_realname}-${pkgver}.tar.gz::https://github.com/s-yata/marisa-trie/archive/v${pkgver}.tar.gz"
       "001-tools-makefile.patch")
sha256sums=('61d222d335db3e8138a22542b95788f307366986c464a68791d361787f3b9c01'
            'd3ec2637462ac3331f0c6bcba9b8552bca4819321df265b3fd895547bbf75717')

prepare() {
  cd "${_realname}-${pkgver}"
  patch -p0 -i ${srcdir}/001-tools-makefile.patch  
  autoreconf -fiv
}

build() {
  [[ -d build-${MINGW_CHOST} ]] && rm -rf build-${MINGW_CHOST}
  mkdir build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  if [ "${CARCH}" = "x86_64" ]; then
    local _conf='--enable-native-code'
  else
    local _conf=''
  fi
  
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    ${_conf} \
    --disable-shared

  make
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
}
